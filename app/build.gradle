apply plugin: 'com.android.application'
apply plugin: 'android-apt'

def gitSha1 = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    // load api key and put it to build config
    Properties localProperties = new Properties()
    localProperties.load(new FileInputStream(file(project.rootProject.file("local.properties"))))

    defaultConfig {
        applicationId "com.bitsailer.yauc"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "UNSPLASH_CLIENT_ID", localProperties["unsplash_client_id"]
        buildConfigField "String", "UNSPLASH_CLIENT_SECRET", localProperties["unsplash_client_secret"]
    }

    signingConfigs {
        config {
            keyAlias 'yauc'
            keyPassword 'password'
            storeFile file("$rootDir/keyStore.jks")
            storePassword 'password'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

ext {
    // versions for libraries with multiple dependencies
    supportLibVersion = '23.4.0'
    schematicVersion = '0.6.7'
    playServicesVersion = '9.4.0'
    butterKnifeVersion = '8.2.1'
    retrofitVersion = '2.1.0'
    okHttpVersion = '3.4.1'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Android Libraries
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"

    // Binding
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    // HTTP/REST client
    compile 'commons-lang:commons-lang:2.6'
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.google.code.gson:gson:2.7"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"

    // Schematic
    apt "net.simonvt.schematic:schematic-compiler:${schematicVersion}"
    compile "net.simonvt.schematic:schematic:${schematicVersion}"

    // Image loading
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.android.support:support-v4:${supportLibVersion}"

    // Google Analytics
    compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"

    // Google Maps
    compile "com.google.android.gms:play-services-maps:${playServicesVersion}"

    // Logging
    compile 'com.orhanobut:logger:1.15'

    // Events
    compile 'org.greenrobot:eventbus:3.0.0'

    // Validation of user input
    compile 'com.mobsandgeeks:android-saripaar:2.0.3'

    // Testing
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'